/*
 * Copyright (c) 2024 General Motors GTO LLC.
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-FileType: DOCUMENTATION
 * SPDX-FileCopyrightText: 2023 General Motors GTO LLC
 * SPDX-License-Identifier: Apache-2.0
 */
plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlin
    id 'maven-publish'
}

apply from: '../gradle/config.gradle'
apply from: '../gradle/kover.gradle'

android {
    compileSdk        config.sdkVersion
    compileSdkVersion config.sdkVersion
    buildToolsVersion config.toolsVersion
    namespace         config.namespace
    buildFeatures.buildConfig true

    defaultConfig {
        minSdk config.minSdkVersion
        targetSdk config.sdkVersion
        buildConfigField 'String', 'VERSION_NAME', /"${config.version}"/
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
        }
    }

    buildFeatures {
        aidl = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    testNamespace "${config.namespace}.test"
    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.all {
            testLogging {
                exceptionFormat 'full'
                events 'passed', 'skipped', 'failed'
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
    }

    publishing {
        singleVariant('release') {
            withJavadocJar()
            withSourcesJar()
        }
    }
}

dependencies {
    implementation libs.androidx.appcompat
    implementation libs.up.kotlin
    implementation libs.coroutine
    testImplementation project(':library')

    androidTestImplementation libs.androidx.espresso
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.junit
    androidTestImplementation libs.mockk.android

    testImplementation project(path: ':library')
    testImplementation libs.androidx.junit
    testImplementation libs.junit
    testImplementation libs.mockk
    testImplementation libs.coroutine.test
    testImplementation libs.robolectric
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = config.group
                artifactId = config.artifact
                version = config.version

                from components.release
            }
        }
    }
}
